


/// Key-value pair used for storing in the Map
class KeyValuePair<Key: Hashable, Value> {

    var key: Key;
    var value: Value;

    init(key: Key, value: Value) {
        this.key = key;
        this.value = value;
    }
}


class Map<Key: Hashable, Value> {

    var hashTable: Array<Array<KeyValuePair<Key, Value> > >;
    var size: uint;

    /// Initializes an empty map
    init() {
        size = 0;
        hashTable = Array<Array<KeyValuePair<Key, Value> > >();
        increaseTableSize(hashTable, 128);
    }

    /// Inserts an element into the map. If the element exists already, nothing is done.
    mutating func insert(key: Key, value: Value) {
        if (contains(key)) {
            return;
        }

        let hashValue = uint(key.hash()) % capacity();
        var pair = KeyValuePair(key, value);

        hashTable[hashValue].push(pair);
        size++;

        if (loadFactor() > 0.66) {
            resize();
        }
    }

    /// Removes an element from the map, if it exists there.
    mutating func remove(e: Key*) {
        let hashValue = uint(e.hash()) % capacity();
        let slot = hashTable[hashValue];

        if (slot.size() == 0) {
            return;
        }

        for (let i in 0..slot.size()) {
            if (slot[i].key == *e) {
                slot.removeAt(i);
                size--;
                return;
            }
        }
    }

    /// Expands the hash table size, needed when there's too many elements in the map
    mutating func increaseTableSize(newTable: mutable Array<Array<KeyValuePair<Key, Value> > >*, newCapacity: uint) {
        for (let i in 0..newCapacity) {
            newTable.push(Array<KeyValuePair<Key, Value> >());
        }
    }

    func size() -> uint {
        return size;
    }

    func capacity() -> uint {
        return hashTable.size();
    }

    /// Resizes the map. This includes copying the old table into a new, bigger one
    mutating func resize() {
        var newTable = Array<Array<KeyValuePair<Key, Value> > >();
        let newCapacity = capacity() * 2;

        increaseTableSize(newTable, newCapacity);

        for (let i in 0..capacity()) {
            let slot = hashTable[i];

            for (let el in slot) {
                newTable[uint(el.key.hash()) % newCapacity].push(*el);
            }
        }

        hashTable = newTable;
    }

    func [](e: Key*) -> Value*? {
        let hashValue = uint(e.hash()) % capacity();
        let slot = hashTable[hashValue];

        for (let element in slot) {
            if (element.key == *e) {
                return &element.value;
            }
        }
        return null;
    }

    /// Checks if e is part of the map.
    func contains(e: Key*) -> bool {
        let hashValue = uint(e.hash()) % capacity();
        let slot = hashTable[hashValue];

        if (slot.size() == 0) {
            return false;
        }

        for (let element in slot) {
            if (element.key == *e) {
                return true;
            }
        }

        return false;
    }

    func isEmpty() -> bool {
        return size == 0;
    }

    /// Returns the load factor for the map. This is used to increase the map size once
    /// the load factor gets too big.
    func loadFactor() -> float64 {
        return float64(size) / (float64(capacity()));
    }

    /// Iterate over the map
    func iterator() -> MapIterator<Key, Value> {
        return MapIterator(this);
    }
}
