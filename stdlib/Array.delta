class Array<Element> {
    var bufferPointer: mutable Element*;
    var size: uint;
    var capacity: uint;

    /// Initializes an empty array.
    init() {
        bufferPointer = undefined;
        size = 0;
        capacity = 0;
    }

    /// Initializes an empty array with pre-allocated capacity.
    init(capacity: uint) {
        // TODO: Call Array.init() when initializers can call other initializers.
        bufferPointer = undefined;
        size = 0;
        this.capacity = 0;

        reserve(capacity);
    }

    /// Initializes an array containing the elements of the given array.
    init(elements: Element[]*) {
        // TODO: Call Array.init() when initializers can call other initializers.
        bufferPointer = undefined;
        size = 0;
        this.capacity = 0;

        reserve(elements.size());

        // TODO: Change this to a for-each loop when arrays can be iterated.
        for (let index in 0..elements.size()) {
            push(elements[index]);
        }
    }

    deinit() {
        free(cast<mutable void*>(bufferPointer));
    }

    /// Returns the number of elements in the array.
    func size() -> uint {
        return size;
    }

    /// Returns true if the array has no elements, otherwise false
    func isEmpty() -> bool {
        return size == 0;
    }

    /// Returns the number of elements the array can store without allocating more memory.
    func capacity() -> uint {
        return capacity;
    }

    /// Returns the element at the given index.
    func [](index: uint) -> Element* {
        if (index >= size) {
            _indexOutOfBounds(index);
        }

        return bufferPointer + index;
    }

    /// Returns the element at the given index.
    mutating func [](index: uint) -> mutable Element* {
        if (index >= size) {
            _indexOutOfBounds(index);
        }

        return bufferPointer + index;
    }

    func first() -> Element* {
        if (size == 0) {
            _emptyArrayFirst();
        }

        return bufferPointer;
    }

    func last() -> Element* {
        if (size == 0) {
            _emptyArrayLast();
        }

        return bufferPointer + size;
    }

    func data() -> Element* {
        return bufferPointer;
    }

    mutating func data() -> mutable Element* {
        return bufferPointer;
    }

    /// Adds the given element to the end of the array.
    mutating func push(newElement: Element) {
        if (size == capacity) {
            _grow();
        }

        *(bufferPointer + size) ~= newElement;
        size++;
    }

    /// Ensures that the capacity is large enough to store the given number of elements.
    mutating func reserve(minimumCapacity: uint) {
        if (minimumCapacity > capacity) {
            let newBufferPointer = cast<mutable Element*>(malloc(sizeof(Element) * uint64(minimumCapacity))!);

            for (let index in 0..size) {
                let source = bufferPointer + index;
                let target = newBufferPointer + index;
                *target ~= *source;
            }

            if (capacity != 0) {
                free(cast<mutable void*>(bufferPointer));
            }

            bufferPointer = newBufferPointer;
            capacity = minimumCapacity;
        }
    }

    /// Removes the first element from the array.
    /// Other elements are moved towards the beginning of the array by one index.
    mutating func removeFirst() {
        if (size == 0) {
            _emptyArrayRemoveFirst();
        }

        _removeAt(0);
    }

    /// Removes the last element from the array.
    mutating func removeLast() {
        if (size == 0) {
            _emptyArrayRemoveLast();
        }

        size--;
        (bufferPointer + size).deinit();
    }

    /// Removes the element at the given index from the array.
    /// Elements following the removed element are moved towards the beginning of the array by one index.
    mutating func removeAt(index: uint) {
        if (index >= size) {
            _indexOutOfBounds(index);
        }

        _removeAt(index);
    }

    mutating func _removeAt(index: uint) {
        (bufferPointer + index).deinit();

        for (let i in (index + 1)..size) {
            let source = bufferPointer + i;
            let target = bufferPointer + (i - 1);
            *target ~= *source;
        }

        size--;
    }

    func iterator() -> ArrayIterator<Element> {
        // TODO: When implicit initializers have been implemented, remove the ArrayRef() call below.
        return ArrayIterator(ArrayRef(this));
    }

    mutating func iterator() -> MutableArrayIterator<Element> {
        return MutableArrayIterator(this);
    }

    func enumerate() -> EnumeratedIterator<Element> {
        return EnumeratedIterator(iterator());
    }

    func map<Input, Output>(transform: (Input) -> Output) -> Array<Output> {
        var output = Array<Output>(capacity: this.size);

        for (let element in this) {
            output.push(transform(element));
        }

        return output;
    }

    func filter<Input>(include: (Input) -> bool) -> Array<Element> {
        var output = Array<Element>();

        for (let element in this) {
            if (include(element)) {
                output.push(*element);
            }
        }

        return output;
    }

    mutating func _grow() {
        if (capacity == 0) {
            reserve(1);
        } else {
            reserve(capacity * 2);
        }
    }

    func _indexOutOfBounds(index: uint) {
        printf("Array index %d is out of bounds, size is %d\n", index, size());
        fatalError();
    }

    func _emptyArrayFirst() {
        printf("Called first() on empty Array\n");
        fatalError();
    }

    func _emptyArrayLast() {
        printf("Called last() on empty Array\n");
        fatalError();
    }

    func _emptyArrayRemoveFirst() {
        printf("Called removeFirst() on empty Array\n");
        fatalError();
    }

    func _emptyArrayRemoveLast() {
        printf("Called removeLast() on empty Array\n");
        fatalError();
    }
}

func ==<T>(a: ArrayRef<T>, b: ArrayRef<T>) -> bool {
    if (a.size() != b.size()) {
        return false;
    }

    for (let i in 0..a.size()) {
        if (*a[i] != *b[i]) {
            return false;
        }
    }

    return true;
}
