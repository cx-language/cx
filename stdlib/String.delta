class String: OutputStream, Printable {
    var characters: Array<char>;

    /// Initializes an empty string.
    init() {
        characters = Array<char>();
        characters.push('\0');
    }

    init(string: StringRef) {
        characters = Array<char>(capacity: string.size() + 1);

        for (let ch in string) {
            characters.push(ch);
        }

        characters.push('\0');
    }

    /// Initializes a string with the characters from a character array of known length.
    init(pointer: char*, length: uint) {
        characters = Array<char>(capacity: length + 1);

        for (let ch in StringRef(pointer, length)) {
            characters.push(ch);
        }

        characters.push('\0');
    }

    /// Initializes a string with the contents of a null-terminated C-style string.
    ///
    /// __Note:__ This initializer has a time complexity of O(n) because it has to calculate the
    /// length of the argument string. If you know the length beforehand, consider using
    /// `String.init(pointer: char*, length: uint)` instead.
    ///
    init(cString: char*) {
        let cStringSize = uint(strlen(cString));
        characters = Array<char>(capacity: cStringSize + 1);

        for (let ch in StringRef(cString, cStringSize)) {
            characters.push(ch);
        }

        characters.push('\0');
    }

    func size() -> uint {
        return characters.size() - 1;
    }

    /// Returns the character at the given index.
    func [](index: uint) -> char {
        return *characters[index];
    }

    /// Returns the string as a C-style, i.e. null-terminated, string.
    /// Modifying `this` after calling this function invalidates the returned pointer.
    func cString() -> char* {
        return data();
    }

    /// Returns a pointer to the first character in the string.
    func data() -> char* {
        return characters.data();
    }

    func isEmpty() -> bool {
        return size() == 0;
    }

    /// Returns a pointer to the first character in the string.
    mutating func data() -> mutable char* {
        return characters.data();
    }

    mutating func push(character: char) {
        *characters[size()] = character;
        characters.push('\0');
    }

    mutating func write(string: StringRef) -> bool {
        for (let ch in string) {
            push(ch);
        }
        return true;
    }

    /// Removes the first character from the string.
    /// Other characters are moved towards the beginning of the string by one index.
    mutating func removeFirst() {
        characters.removeFirst();
    }

    /// Removes the last character from the string.
    mutating func removeLast() {
        characters.removeAt(characters.size() - 2);
    }

    /// Supports using strings with sets and dicts
    func hash() -> uint64 {
        return StringRef(this).hash();
    }

    func iterator() -> StringIterator {
        // TODO: When implicit initializers have been implemented, remove the StringRef() call below.
        return StringIterator(StringRef(this));
    }

    /// Returns the index of the given character, or -1 if it's not found
    func find(c: char) -> int {
        return find(c, 0);
    }

    /// Returns the index of the given character, or -1 if it's not found.
    /// Starts from start
    func find(c: char, start: uint) -> int {

        for (let i in start..size()) {
            if (*characters[i] == c) {
                return int(i);
            }
        }
        return -1;
    }

    // Returns the substring of the string in the given range, [inclusive, exclusive]
    func substr(range: Range<int>) -> StringRef {
        if (range.end > int(size())) {
            _indexOutOfBounds(range.end);
        }
        return StringRef(characters[uint(range.start)], uint(range.size()));
    }

    // Returns the substring of the string in the given range, [inclusive, exclusive]
    func substr(range: Range<uint>) -> StringRef {
        if (range.end > size()) {
            _indexOutOfBounds(range.end);
        }
        return StringRef(characters[range.start], range.size());
    }

    // Splits the string by the given delimiter
    func split(delim: char) -> Array<StringRef> {
        var tokens = Array<StringRef>();

        var prev: int = 0;
        var pos: int = 0;

        while (true) {
            pos = find(delim, uint(prev));

            if (pos == -1) {
                pos = int(size());
            }

            let token = substr(prev..pos);
            tokens.push(token);

            prev = pos + 1;

            if (pos > int(size()) || prev > int(size())) {
                break;
            }
        }
        return tokens;
    }

    // Splits the string by whitespace
    func split() -> Array<StringRef> {
        return split(' ');
    }

    func printTo(stream: mutable String*) {
        stream.write(StringRef(this));
    }

    func _indexOutOfBounds(index: uint) {
        printf("Range index %d is out of bounds, size is %d\n", index, size());
        fatalError();
    }

    func _indexOutOfBounds(index: int) {
        printf("Range index %d is out of bounds, size is %d\n", index, size());
        fatalError();
    }

}

func +(a: StringRef, b: StringRef) -> String {
    var result = String();

    for (let ch in a) {
        result.push(ch);
    }

    for (let ch in b) {
        result.push(ch);
    }

    return result;
}

func +(a: String, b: StringRef) -> String {
    var result = a;

    for (let ch in b) {
        result.push(ch);
    }

    return result;
}

func ==(a: StringRef, b: StringRef) -> bool {
    if (a.size() != b.size()) {
        return false;
    }

    // TODO: Change this loop to use zip iterator when it's added.
    for (let index in 0..a.size()) {
        if (a[index] != b[index]) {
            return false;
        }
    }

    return true;
}

func ==(a: String*, b: String*) -> bool {
    return StringRef(a) == StringRef(b);
}

func ==(a: String*, b: StringRef) -> bool {
    return StringRef(a) == b;
}

func ==(a: StringRef, b: String*) -> bool {
    return a == StringRef(b);
}

func <(a: StringRef, b: StringRef) -> bool {
    if (a.size() != b.size()) {
        return a.size() < b.size();
    }

    // TODO: Change this loop to use zip iterator when it's added.
    for (let index in 0..a.size()) {
        if (a[index] < b[index]) {
            return true;
        }
        if (a[index] > b[index]) {
            return false;
        }
    }

    return false;
}
