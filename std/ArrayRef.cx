struct ArrayRef<Element>: Copyable {
    Element[*] data;
    int size;

    /// Initializes an ArrayRef referring to an empty array.
    ArrayRef() {
        data = undefined;
        size = 0;
    }

    /// Initializes an ArrayRef referring to the elements of the given List.
    ArrayRef(List<Element>* list) {
        data = list.data();
        size = list.size();
    }

    /// Initializes an ArrayRef referring to an array of the given size located at the given memory
    /// address.
    ArrayRef(Element[*] data, int size) {
        this.data = data;
        this.size = size;
    }

    bool empty() {
        return size == 0;
    }

    /// Returns the number of elements in the array.
    int size() {
        return size;
    }

    /// Returns the first element in the array.
    Element* front() {
        if (empty()) indexOutOfBounds("front", 0);
        return data[0];
    }

    /// Returns a reference to the element at the given index.
    Element* operator[](int index) {
        if (index < 0 || index >= size()) indexOutOfBounds("operator[]", index);
        return data[index];
    }

    Element[*] data() {
        return data;
    }

    ArrayIterator<Element> iterator() {
        return ArrayIterator(this);
    }

    Element*? find(bool(Element*) predicate) {
        for (var element in this) {
            if (predicate(element)) {
                return element;
            }
        }

        return null;
    }

    // TODO: Remove these once implicit into-iterator conversions have been implemented.
    bool all(bool(Element*) predicate) { return all(iterator(), predicate); }
    bool any(bool(Element*) predicate) { return any(iterator(), predicate); }
    bool none(bool(Element*) predicate) { return none(iterator(), predicate); }

    private void indexOutOfBounds(string function, int index) {
        abort("ArrayRef.", function, ": index ", index, " is out of bounds, size is ", size());
    }
}

bool operator==<T>(ArrayRef<T> a, ArrayRef<T> b) {
    if (a.size() != b.size()) {
        return false;
    }

    for (var i in 0..a.size()) {
        if (a[i] != b[i]) {
            return false;
        }
    }

    return true;
}
