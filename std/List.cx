struct List<Element> {
    ElementSUOJA buffer;
    int size;
    int capacity;

    /// Initializes an empty list.
    List() {
        buffer = undefined;
        size = 0;
        capacity = 0;
    }

    /// Initializes an empty list with pre-allocated capacity.
    List(public int capacity) {
        init();
        reserve(capacity);
    }

    /// Initializes an list containing the elements of the given array.
    List(Element[] elements) {
        init(capacity: elements.size());

        // TODO: When array references can be iterated, iterate 'elements' directly.
        // TODO: Add push() overload for pushing all elements from an array reference.
        for (var index in 0..elements.size()) {
            push(elements[index]);
        }
    }

    /// Initializes the list to contain the given number of uninitialized elements.
    List(public int uninitializedSize) {
        buffer = allocateArray<Element>(uninitializedSize);
        size = uninitializedSize;
        capacity = uninitializedSize;
    }

    ~List() {
        if (capacity != 0) {
            for (var element in this) {
                element.deinit();
            }
            deallocate(buffer);
        }
    }

    /// Returns the number of elements in the list.
    int size() {
        return size;
    }

    /// Returns true if the list has no elements, otherwise false
    bool empty() {
        return size == 0;
    }

    /// Returns the number of elements the list can store without allocating more memory.
    int capacity() {
        return capacity;
    }

    /// Returns the element at the given index.
    Element& operator[](int index) {
        if (index >= size) {
            indexOutOfBounds(index);
        }

        return buffer[index];
    }

    void operator[]=(int index, Element element) {
        buffer[index] = element;
    }

    Element& first() {
        if (size == 0) abort("Called first() on empty List\n");
        return buffer[0];
    }

    Element& last() {
        if (size == 0) abort("Called last() on empty List\n");
        return buffer[size - 1];
    }

    ElementSUOJA data() {
        return buffer;
    }

    /// Adds the given element to the end of the list.
    void push(Element element) {
        if (size == capacity) {
            grow();
        }

        // TODO: Shouldn't need '&' here.
        (&buffer[size]).init(element);
        size++;
    }

    /// Ensures that the capacity is large enough to store the given number of elements.
    void reserve(int minimumCapacity) {
        if (minimumCapacity > capacity) {
            var newBuffer = allocateArray<Element>(size: minimumCapacity);

            for (var index in 0..size) {
                var source = &buffer[index];
                var target = &newBuffer[index];
                target.init(*source);
            }

            if (capacity != 0) {
                deallocate(buffer);
            }

            buffer = newBuffer;
            capacity = minimumCapacity;
        }
    }

    /// Removes the first element from the list.
    /// Other elements are moved towards the beginning of the list by one index.
    void removeFirst() {
        if (size == 0) abort("Called removeFirst() on empty List\n");
        unsafeRemoveAt(0);
    }

    /// Removes the first element satisfying the given condition.
    void removeFirst(bool(Element&) shouldRemove) {
        for (var index in 0..size) {
            if (shouldRemove(this[index])) {
                unsafeRemoveAt(index);
                break;
            }
        }
    }

    /// Removes the last element from the list.
    void removeLast() {
        if (size == 0) abort("Called removeLast() on empty List\n");
        size--;
        buffer[size].deinit();
    }

    /// Removes and returns the last element.
    Element pop() {
        if (size == 0) abort("Called pop() on empty List\n");
        size--;
        return buffer[size];
    }

    /// Removes the element at the given index from the list.
    /// Elements following the removed element are moved towards the beginning of the list by one index.
    void removeAt(int index) {
        if (index >= size) {
            indexOutOfBounds(index);
        }

        unsafeRemoveAt(index);
    }

    private void unsafeRemoveAt(int index) {
        buffer[index].deinit();

        for (var i in (index + 1)..size) {
            var source = &buffer[i];
            var target = &buffer[i - 1];
            target.init(*source);
        }

        size--;
    }

    ArrayIterator<Element> iterator() {
        // TODO: When implicit constructors have been implemented, remove the ArrayRef() call below.
        return ArrayIterator(ArrayRef(this));
    }

    EnumeratedIterator<Element> enumerate() {
        return EnumeratedIterator(iterator());
    }

    Element&? find(bool(Element&) predicate) {
        for (var element in this) {
            if (predicate(element)) {
                return element;
            }
        }

        return null;
    }

    List<Output> map<Output>(Output(Element&) transform) {
        var output = List<Output>(capacity: this.size);

        for (var element in this) {
            output.push(transform(element));
        }

        return output;
    }

    List<Output> map<Output>(Output(Element) transform) {
        var output = List<Output>(capacity: this.size);

        for (var element in this) {
            output.push(transform(element));
        }

        return output;
    }

    List<Element> filter(bool(Element&) include) {
        var output = List<Element>();

        for (var element in this) {
            if (include(element)) {
                output.push(element);
            }
        }

        return output;
    }

    List<Element> filter(bool(Element) include) {
        var output = List<Element>();

        for (var element in this) {
            if (include(element)) {
                output.push(element);
            }
        }

        return output;
    }

    // TODO: Remove these once implicit into-iterator conversions have been implemented.
    bool all(bool(Element*) predicate) { return all(iterator(), predicate); }
    bool any(bool(Element*) predicate) { return any(iterator(), predicate); }
    bool none(bool(Element*) predicate) { return none(iterator(), predicate); }

    private void grow() {
        if (capacity == 0) {
            reserve(1);
        } else {
            reserve(capacity * 2);
        }
    }

    private void indexOutOfBounds(int index) {
        abort("List index ", index, " is out of bounds, size is ", size());
    }
}

bool operator==<T>(List<T>& a, List<T>& b) {
    return ArrayRef(a) == ArrayRef(b);
}
